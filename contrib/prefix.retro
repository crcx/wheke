( ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ )
( One of the more interesting features in Retro 9.x was the   )
( support for appending single character prefixes to words to )
( alter their behaviour.                                      )
(                                                             )
( The rewritten Retro codebase dropped prefixes, but is clean )
( and flexible enough to allow them to be supported using a   )
( custom 'notfound' replacement.                              )
(                                                             )
( Prefixes provided by this code:                             )
(   @ - Get the value of a variable                           )
(   ! - Set the value of a variable                           )
(   ` - Invoke the word as if it were a macro word            )
(   ~ - Compile the address of a word [or leave it on the     )
(       stack if interpreting]                                )
(                                                             )
( This code requires the 'find' word to be loaded prior to    )
( running.                                                    )
( ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ )


{{
  variable code
  variable class
  macro: char: 32 accept tib @ .data ;
  : handle
    dup d->class @ class ! d->xt @
    char: @ code @ =if @ ;then
    char: ! code @ =if ! ;then
    char: ` code @ =if execute ;then
    char: ~ code @ =if .data ;then
    class @ with-class
  ;
---reveal---
  : prefix tib @ code ! tib 1+ find if handle ;then drop ." Not found!" cr ;
}}
' prefix is notfound
