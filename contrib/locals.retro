( ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ )
( Really simple [and limited] implementation of locals.       )
( This is adapted from some code written for Retro 7; use at  )
( your own risk.                                              )
( ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ )
( Sample Use:                                                 )
(  : foo [local !a !b  @a @b * . local] ;                     )
( Stack comment for example:  ba-                             )
(                                                             )
( This code provides four variables [a, b, c, d] allocated on )
( the address stack and initialized from values on the data   )
( stack.                                                      )
(                                                             )
( This is just tested enough to ensure that it functions. No  )
( official support is provided; and [aside from applying any  )
( third-party patches], no future development is likely to be )
( done on it.                                                 )
( ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ )
{{
  variable rsp
  variable a

  : rdepth -6 5 out wait 5 in ;

---reveal---
  : [local rdepth rsp ! ;
  : local] rdepth rsp @ - for pop pop pop . push push next ;

  : !a a ! pop a @ push push ;
  : !b a ! pop pop a @ push push push ;
  : !c a ! pop pop pop a @ push push push push ;
  : !d a ! pop pop pop pop a @ push push push push push ;

  : @a pop pop dup a ! push push a @ ;
  : @b pop pop pop dup a ! push push push a @ ;
  : @c pop pop pop pop dup a ! push push push push a @ ;
  : @d pop pop pop pop pop dup a ! push push push push push a @ ;
}}
