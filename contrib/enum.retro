( ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ )
( Enumerated Sequences of Values                              )
( ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ )
( Sample Use:                                                 )
(  0 enum| 1+ a b c d e f g |                                 )
(                                                             )
( The initial value is taken from the stack, the first word   )
( following enum| is called to update the value for the next  )
( created value. This is followed by a whitespace delimited   )
( sequence of names, and the list ends with a | character.    )
( ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ )
{{
  variable op
  : begin ' op ! ;
  : end? last @ d->name s" |" compare ;
  : update dup last @ d->xt ! op @ execute ;
  : cleanup last @ heap ! drop last @ @ last ! ;
---reveal---
  : enum| begin repeat create end? if cleanup ;then update again ;
}}
