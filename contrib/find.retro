( ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ )
( Retro does not ship with a visible word for searching the   )
( dictionary for specific words. This adds one.               )
(                                                             )
( It takes a string containing the name of the word to look   )
( for and returns an address and a flag. If true, the address )
( is the *dictionary header* of the word being looked for. If )
( false, just discard the address.                            )
( ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ )
{{
  variable name
  variable found
  : prepare ( a-a )  found off name ! last @ ;
  : done    ( -af )  which @ found @ ;
  : match?  ( $-$f ) dup d->name name @ compare ;
  : search  ( $- )   repeat match? if which ! found on ;then @ 0; again ;
---reveal---
  : find ( "-af ) prepare search done ;
}}
