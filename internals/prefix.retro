( One of the more interesting features in Retro 9.x was the   )
( support for appending single character prefixes to words to )
( alter their behaviour.                                      )
(                                                             )
( The rewritten Retro codebase dropped prefixes, but is clean )
( and flexible enough to allow them to be supported using a   )
( custom 'notfound' replacement.                              )
(                                                             )
( Prefixes provided by this code:                             )
(   @ - Get the value of a variable                           )
(   ! - Set the value of a variable                           )
(   ` - Invoke the word as if it were a macro word            )
(   ~ - Compile the address of a word [or leave it on the     )
(       stack if interpreting]                                )
(   ' - Get the value of the following char.                  )
(                                                             )
( This code requires the 'find' word to be loaded prior to    )
( running.                                                    )
( ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ )
( Copyright [c] 2009, Charles Childers                        )
( License: ISC                                                )
( ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ )


{{
  variable code
  variable class
  : char: 32 accept tib @ .data ; immediate
  : handle
    dup d->class @ class ! d->xt @
    char: @ code @ =if @ ;then
    char: ! code @ =if ! ;then
    char: ` code @ =if execute ;then
    char: ~ code @ =if .data ;then
    class @ with-class
  ;
  : otherwise
    drop
    char: ' code @ =if tib 1+ @ ;then
    ." Not Found" cr
  ;
---reveal---
  : prefix tib @ code ! tib 1+ find if handle ;then otherwise ;
}}
' prefix is notfound
