( One of the more interesting features in Retro 9.x was the   )
( support for appending single character prefixes to words to )
( alter their behaviour.                                      )
(                                                             )
( The rewritten Retro codebase dropped prefixes, but is clean )
( and flexible enough to allow them to be supported using a   )
( custom 'notfound' replacement.                              )
(                                                             )
( Prefixes provided by this code:                             )
(   @ - Get the value of a variable                           )
(   ! - Set the value of a variable                           )
(   ` - Invoke the word as if it were a macro word            )
(   ~ - Compile the address of a word [or leave it on the     )
(       stack if interpreting]                                )
(   ' - Get the value of the following char.                  )
(                                                             )
( This code requires the 'find' word to be loaded prior to    )
( running.                                                    )
( ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ )
( Copyright [c] 2009, Charles Childers                        )
( License: ISC                                                )
( ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ )


{{
  variable code
  variable class
  : clear 0 code ! ;
  : cond: ` char: ` code ` @ ` =if ; compile-only
  : ;cond ` clear ` ;then ; compile-only
  : always
    cond: ' tib 1+ @ ;cond
    cond: " tib 1+ keepString ;cond
  ;
  : if-word
    code @ 0; drop
    tib 1+ find if
      which @ dup d->class @ class ! d->xt @
      cond: @  @       ;cond
      cond: !  !       ;cond
      cond: `  execute ;cond
      cond: ~  .data   ;cond
      class @ with-class
    then
  ;
  : otherwise
    code @ 0; drop
    ." Not Found" cr
  ;
---reveal---
  : prefix tib @ code ! always if-word otherwise ;
}}
' prefix is notfound
